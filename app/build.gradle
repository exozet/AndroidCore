import java.security.spec.InvalidKeySpecException

ext {
    versionName = VERSION_NAME
    VERSION_CODE = 1
    VSC = VSC_PATH
    isCi = true
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

android {

    // parcilze
    androidExtensions {
        experimental = true
    }

    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId APPLICATION_ID
        multiDexEnabled true

        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode commitCount()
        versionName simpleReleaseVersionName()
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "SIMPLE_VERSION_NAME", escape(isCi ? simpleReleaseVersionName() : "")
        buildConfigField "String", "CANONICAL_VERSION_NAME", escape(isCi ? canonicalReleaseVersionName() : "")
        buildConfigField "String", "COMMIT_HASH", escape(isCi ? commitHash() : "")
        buildConfigField "String", "BRANCH", escape(isCi ? branchNameByGit() : "")
        buildConfigField "String", "BUILD_DATE", escape(isCi ? new Date().getTime() : 0)
        buildConfigField "String", "VSC", escape(isCi ? VSC_PATH : "")
    }
    signingConfigs {
        try {
            debug {
                storeFile file(DEBUG_KEYSYORE_PATH)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEYSTORE_ALLIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
        catch (final ignored) {
            throw new InvalidKeySpecException('You should define DEBUG_KEYSTORE_ALLIAS, DEBUG_STORE_PASSWORD, DEBUG_KEY_PASSWORD in gradle.properties.')
        }
        try {
            release {
                storeFile file(RELEASE_KEYSYORE_PATH)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEYSTORE_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        } catch (final ignored) {
            throw new InvalidKeySpecException('You should define RELEASE_KEYSTORE_ALIAS, RELEASE_STORE_PASSWORD, RELEASE_KEY_PASSWORD in gradle.properties.')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            shrinkResources = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    realm {
        kotlinExtensionsEnabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // region test options

    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            ignoreFailures = true
        }
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    // region compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // endregion

    // region compile options

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    // endregion

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
        tasks.lint.enabled = false
    }

    // endregion

    // region dex options

    dexOptions {
        javaMaxHeapSize "4g"
        maxProcessCount 8
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
        exclude 'META-INF/library-core_release.kotlin_module'
    }

    // endregion
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation libs.supportMultidex

    // Core library
    androidTestImplementation androidTestLibs.testCore

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation androidTestLibs.espressoRunner
    androidTestImplementation androidTestLibs.espressoRules
    androidTestUtil androidTestUtilLibs.orchestrator

    // Assertions
    androidTestImplementation androidTestLibs.testJUnit
    androidTestImplementation androidTestLibs.truth

    // Espresso dependencies
    androidTestImplementation androidTestLibs.espressoCore
    androidTestImplementation androidTestLibs.espressoContrib
    androidTestImplementation androidTestLibs.espressoIntents
    androidTestImplementation androidTestLibs.espressoAccessibility
    androidTestImplementation androidTestLibs.espressoWeb
    androidTestImplementation androidTestLibs.idlingConcurrent
    androidTestImplementation androidTestLibs.espressoIdlingResource
    androidTestImplementation androidTestLibs.fragmentTesting

    testImplementation testLibs.junit
    testImplementation testLibs.jsonUnit
    testImplementation testLibs.jsonUnitFluent
    testImplementation testLibs.robolectric
    testImplementation androidTestLibs.truth
    testImplementation androidTestLibs.testCore

    implementation libs.androidKtxCore
    implementation libs.supportAppCompat
    implementation libs.appCompatResources
    implementation libs.fragmentKtx
    implementation libs.activityKtx
    implementation libs.material
    implementation libs.swipeRefreshLayout
    implementation libs.constraintLayout2
    implementation libs.supportRecyclerview
    implementation libs.supportAnnotations
    implementation libs.supportVectorDrawable
    implementation libs.androidKtxPalette
    implementation libs.supportMedia

    implementation project(':core')
    implementation libs.logger
    implementation libs.timberLogger
    implementation libs.parceler
    kapt libs.parcelerProcessor

    implementation libs.recyclerViewPresenter4
    implementation libs.lifecycleExtensions
    implementation libs.gson

    implementation libs.rx2java
    implementation libs.rx2Android
    implementation libs.retrofitAdapterRxJava2
    implementation libs.retrofit
    implementation libs.connectionBuddy
    implementation libs.googlePlayForceUpdate
    implementation libs.timeBomb
    implementation libs.playServicesAuth
    implementation libs.firebaseDatabase
    implementation libs.firebaseMessaging
    implementation libs.firebaseJobdispatcher
    implementation libs.apacheCommonsIo
    implementation libs.realmExtensions
    implementation libs.joda

    implementation libs.secureStorage
}

// region override support library version

configurations.all {

    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'

    resolutionStrategy.force libs.kotlinJdk8
    resolutionStrategy.force libs.androidKtxCore
    resolutionStrategy.force libs.supportAppCompat
    resolutionStrategy.force libs.appCompatResources
    resolutionStrategy.force libs.fragmentKtx
    resolutionStrategy.force libs.activityKtx
    resolutionStrategy.force libs.material
    resolutionStrategy.force libs.swipeRefreshLayout
    resolutionStrategy.force libs.constraintLayout2
    resolutionStrategy.force libs.supportRecyclerview
    resolutionStrategy.force libs.supportAnnotations
    resolutionStrategy.force libs.supportVectorDrawable
    resolutionStrategy.force libs.androidKtxPalette
    resolutionStrategy.force libs.supportMedia
    resolutionStrategy.force libs.lifecycleExtensions
    resolutionStrategy.force libs.lifecycleCommonJava8
    resolutionStrategy.force libs.lifecycleLivedataKtx

    resolutionStrategy.force libs.gson
}

// endregion

static def escape(Object value) {
    return "\"$value\""
}


task copyReadme(type: Copy) {
    from "${project.rootDir}/README.md"
    into "${project.rootDir}/app/src/main/assets"
}

task('generateChangelogTask') {
    generateChangelog()
}

task updateLocalization(type: Exec) {
    commandLine 'bash', './updateLocalization.sh'
}

preBuild.dependsOn generateChangelogTask
preBuild.dependsOn copyReadme
//preBuild.dependsOn updateLocalization

apply plugin: 'com.google.gms.google-services'